// RN75+_BLOCK_START
pluginManagement { includeBuild("../node_modules/@react-native/gradle-plugin") }
plugins { id("com.facebook.react.settings") }
extensions.configure(com.facebook.react.ReactSettingsExtension) { ex -> ex.autolinkLibrariesFromCommand() }
// RN75+_BLOCK_END

apply from: file("../../android/rninfo.gradle")
println "[settings] RNInfo: detected React Native version: (major=${ext.rnInfo.version})"

if (!ext.rnInfo.isRN75OrHigher) {
    // For versions below 0.75, we need to apply the old plugin
    apply from: file("../node_modules/@react-native-community/cli-platform-android/native_modules.gradle");
    applyNativeModulesSettingsGradle(settings)
}

rootProject.name = 'DetoxTest'
include ':app'

if (ext.rnInfo.isRN72OrHigher) {
    includeBuild('../node_modules/@react-native/gradle-plugin')
} else {
    includeBuild('../node_modules/react-native-gradle-plugin')
}

include ':detox'
project(':detox').projectDir = new File(rootProject.projectDir, '../../android/detox')


includeBuild('../node_modules/react-native') {
    dependencySubstitution {
        substitute(module("com.facebook.react:react-android")).using(project(":packages:react-native:ReactAndroid"))
        substitute(module("com.facebook.react:react-native")).using(project(":packages:react-native:ReactAndroid"))
        substitute(module("com.facebook.react:hermes-android")).using(project(":packages:react-native:ReactAndroid:hermes-engine"))
        substitute(module("com.facebook.react:hermes-engine")).using(project(":packages:react-native:ReactAndroid:hermes-engine"))
    }
}
